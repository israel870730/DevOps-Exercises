module "eks_blueprints" {
  source = "github.com/aws-ia/terraform-aws-eks-blueprints?ref=v4.25.0"

  cluster_name = var.cluster_name
  # EKS Cluster VPC and Subnet mandatory config
  vpc_id             = var.vpc_id
  private_subnet_ids = var.private_subnets
  # EKS CONTROL PLANE VARIABLES
  cluster_version                        = var.cluster_version
  cluster_service_ipv4_cidr              = var.service_cidr_block
  cloudwatch_log_group_retention_in_days = var.cloudwatch_log_group_retention_in_days
  cluster_enabled_log_types              = ["audit", "api", "authenticator", "controllerManager", "scheduler"]
  cluster_endpoint_public_access         = var.cluster_endpoint_public_access
  cluster_endpoint_private_access        = var.cluster_endpoint_private_access

  #EKS Managed Nodes in AZ
  # managed_node_groups = {
  #   mng = {
  #     node_group_name = "mng"
  #     name            = "mng"
  #     instance_types  = ["m5.large"]
  #     min_size        = "1"
  #     max_size        = "5"
  #     desired_size    = "1"
  #     subnet_ids      = var.private_subnets
  #     tags            = {
  #       Name = "mng"
  #     }
  #   }
  # }
  
  #EKS LOCAL ZONE NODE GROUP
  self_managed_node_groups = {
    smng = {
      #custom_ami_id        = ""          # Bring your own custom AMI generated by Packer/ImageBuilder/Puppet etc.
      node_group_name       = "smng"
      instance_type         = "t3.medium" # Tipo de instancia soportado en la zona local de Buenos Aires
      subnet_ids            = [var.private_subnets_local_zone]
      launch_template_os    = "amazonlinux2eks" # amazonlinux2eks  or bottlerocket or windows
      #key_name              = ""
      enable_monitoring     = true
      # AUTOSCALING
      min_size              = "1"
      max_size              = "5"
      desired_size          = "2"
      block_device_mappings = [
        {
          device_name = "/dev/xvda"
          volume_type = "gp2"
          volume_size = "20"
        },
      ]
    },
  }

  cluster_security_group_additional_rules = {
      ingress_nodes = {
      description                = "Allow all connections from nodes"
      protocol                   = "-1"
      from_port                  = 0
      to_port                    = 0
      type                       = "ingress"
      source_node_security_group = true
    }
  }

  #----------------------------------------------------------------------------------------------------------#
  # Securaity groups used in this module created by the upstream modules terraform-aws-eks (https://github.com/terraform-aws-modules/terraform-aws-eks).
  #   Upstrem module implemented Security groups based on the best practices doc https://docs.aws.amazon.com/eks/latest/userguide/sec-group-reqs.html.
  #   So, by default the security groups are restrictive. Users needs to enable rules for specific ports required for App requirement or Add-ons
  #   See the notes below for each rule used in these examples
  #----------------------------------------------------------------------------------------------------------#
  node_security_group_additional_rules = {
    # Extend node-to-node security group rules. Recommended and required for the Add-ons
    ingress_self_all = {
      description = "Node to node all ports/protocols"
      protocol    = "-1"
      from_port   = 0
      to_port     = 0
      type        = "ingress"
      self        = true
    }
    # Recommended outbound traffic for Node groups
    egress_all = {
      description      = "Node all egress"
      protocol         = "-1"
      from_port        = 0
      to_port          = 0
      type             = "egress"
      cidr_blocks      = ["0.0.0.0/0"]
      ipv6_cidr_blocks = ["::/0"]
    }
    # Custom rule
    egress_artifactory = {
      description      = "CIDR artifactory"
      protocol         = "tcp"
      from_port        = 80
      to_port          = 80
      type             = "egress"
      cidr_blocks      = ["10.2.0.0/24"]
    }
    # Allows Control Plane Nodes to talk to Worker nodes on all ports. Added this to simplify the example and further avoid issues with Add-ons communication with Control plane.
    # This can be restricted further to specific port based on the requirement for each Add-on e.g., metrics-server 4443, spark-operator 8080, karpenter 8443 etc.
    # Change this according to your security requirements if needed
    ingress_cluster_to_node_all_traffic = {
      description                   = "Cluster API to Nodegroup all traffic"
      protocol                      = "-1"
      from_port                     = 0
      to_port                       = 0
      type                          = "ingress"
      source_cluster_security_group = true
    }
  }

}


module "eks_blueprints_kubernetes_addons" {
  source = "github.com/aws-ia/terraform-aws-eks-blueprints//modules/kubernetes-addons?ref=v4.32.1"

  eks_cluster_id               = module.eks_blueprints.eks_cluster_id
  eks_worker_security_group_id = module.eks_blueprints.worker_node_security_group_id
  auto_scaling_group_names     = module.eks_blueprints.self_managed_node_group_autoscaling_groups
  eks_cluster_domain = local.domain_for_route53

  # EKS Addons
  # TODO: Took ~15 min to deploy addons, need to debug further 
  enable_amazon_eks_vpc_cni            = true
  #enable_amazon_eks_coredns            = true
  enable_amazon_eks_kube_proxy         = true
  enable_aws_load_balancer_controller  = true  
  enable_metrics_server                = true
  enable_aws_efs_csi_driver            = true  # EFS CSI Drvier required two nodes so that installing helm chart will not stuck

  enable_aws_cloudwatch_metrics = true
  enable_aws_for_fluentbit      = true
  aws_for_fluentbit_helm_config = {
    name        = "aws-for-fluent-bit"
    namespace   = "fluentbit"
    description = "The AWS Fluent-bit Helm chart deployment configuration"
    values = [templatefile("${path.module}/values/fluentbit_values.yaml", {
      region = var.region
      logGroupName = "${module.eks_blueprints.eks_cluster_id}-fluentbit"
      bucket_name =  local.bucket_name
      clusterName = "${module.eks_blueprints.eks_cluster_id}"
    })]
  }

  enable_coredns_autoscaler            = false
  enable_coredns_cluster_proportional_autoscaler = false

  depends_on = [
    module.eks_blueprints.self_managed_node_groups
  ]

}

#Agregar reglas al SG del cluster
resource "aws_security_group_rule" "allow_node_sg_to_cluster_sg" {
  description = "Self-managed Nodegroup to Cluster API/Managed Nodegroup all traffic"

  source_security_group_id = module.eks_blueprints.worker_node_security_group_id
  security_group_id        = module.eks_blueprints.cluster_primary_security_group_id
  type                     = "ingress"
  protocol                 = "-1"
  from_port                = 0
  to_port                  = 0

  depends_on = [
    module.eks_blueprints
  ]
}

resource "aws_security_group_rule" "allow_node_sg_from_cluster_sg" {
  description = "Cluster API/Managed Nodegroup to Self-Managed Nodegroup all traffic"
  source_security_group_id = module.eks_blueprints.cluster_primary_security_group_id
  security_group_id        = module.eks_blueprints.worker_node_security_group_id
  type                     = "ingress"
  protocol                 = "-1"
  from_port                = 0
  to_port                  = 0

  depends_on = [
    module.eks_blueprints
  ]
}


# resource "null_resource" "kubeconfig" {
#   provisioner "local-exec" {
#     command = "aws eks update-kubeconfig --region ${local.region} --name ${module.eks_blueprints.eks_cluster_id}"
#   }

#   depends_on = [
#     module.eks_blueprints
#   ]
# }

#Agregar reglas al SG de los nodos
resource "aws_security_group_rule" "worker_node_sg_update_egress" {
  depends_on = [module.eks_blueprints]
  description = "Allow artifactory.vficloud.net CIDR"
  from_port         = 443
  to_port           = 443
  protocol          = "tcp"
  cidr_blocks       = var.artifactory
  type              = "egress"
  security_group_id = module.eks_blueprints.worker_node_security_group_id
}

resource "aws_security_group_rule" "worker_node_sg_update_ingress" {
  depends_on = [module.eks_blueprints]
  description = "Allow artifactory.vficloud.net CIDR"
  from_port         = 443
  to_port           = 443
  protocol          = "tcp"
  cidr_blocks       = var.artifactory
  type              = "ingress"
  security_group_id = module.eks_blueprints.worker_node_security_group_id
}

module "eks_auth" {
  source     = "../../4.1-VPC+EFS-EKS-LocalZone/4-aws-auth/"
  eks        = module.eks_blueprints
  depends_on = [null_resource.kubeconfig]

  map_roles = [
    {
      rolearn  = "arn:aws:iam::012345678901:role/TerraformRole-Eks"
      username = "admin-role"
      groups   = ["system:masters"]
    }
  ]

  map_users = [
    {
      userarn = "arn:aws:iam::012345678901:root"
      username = "root"
      groups   = ["system:masters"]
    },
    {
      userarn = "arn:aws:iam::012345678901:user/poc"
      username = "poc"
      groups   = ["system:masters"]
    }
  ]

  map_accounts = []
}

resource "null_resource" "kubeconfig" {
  depends_on = [module.eks_blueprints]

  provisioner "local-exec" {
    interpreter = ["/bin/bash", "-c"]
    command = <<EOT
      set -e
      echo 'Updating Kube config file'
      # aws sts get-caller-identity
      export $(printf "AWS_ACCESS_KEY_ID=%s AWS_SECRET_ACCESS_KEY=%s AWS_SESSION_TOKEN=%s" \
             $(aws sts assume-role \
                   --role-arn ${var.terraformrole} \
                   --role-session-name TerraformRole \
                   --query "Credentials.[AccessKeyId,SecretAccessKey,SessionToken]" \
                   --output text))
      aws sts get-caller-identity
      aws eks wait cluster-active --name '${module.eks_blueprints.eks_cluster_id}'  --region '${local.region}'
      aws eks --region ${local.region} update-kubeconfig --name ${module.eks_blueprints.eks_cluster_id} --alias ${module.eks_blueprints.eks_cluster_id}
    EOT
  }
}
